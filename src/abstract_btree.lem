open import Pervasives Set_extra Assert_extra Maybe_extra

module Prelude = struct

  val rev_apply : forall 'a 'b.
    'a ->
    ('a -> 'b) ->
    'b
  let rev_apply x f = f x

  type rresult 'a = Ok of 'a | Error

  val rresult_to_option : forall 'a.
    rresult 'a ->
    maybe 'a
  let rresult_to_option r = (
      match r with
      | Ok r' -> Just r'
      | Error -> Nothing
      end)

  val from_n_to_n':
    nat ->
    nat ->
    list nat
  let rec from_n_to_n' n n'=
    (match n' with
     | 0 -> []
     | _ ->
        let j = n' - 1 in
        (if n <= j then from_n_to_n' n j ++ [j] else []) 
     end)
  declare isabelle target_rep function from_n_to_n' = `List.upt`
end

module Store = struct
  open Prelude

  type page 'bs = Page of 'bs (* bytes *)

  type page_ref 'r = Page_ref of 'r

  val dest_page_ref : forall 'r.
    page_ref 'r ->
    'r
  let dest_page_ref (Page_ref r) = r


  type store 'r 'bs =
    <|
     store_last_free_ref  : page_ref 'r; (* if the store is empty this should be undefined *)
     store_map            : (map (page_ref 'r) (page 'bs)); (* different to paper: we store actual bytes *)
    |>

  val ref_to_page : forall 'r 'bs.
    store 'r 'bs ->
    page_ref 'r ->
    maybe (page 'bs)
  let ref_to_page s pr = Map.lookup pr (s.store_map)

  val insert_store : forall 'r 'bs.
    store 'r 'bs ->
    (page_ref 'r * page 'bs) ->
    store 'r 'bs
  let insert_store s0 rp =
    let (r,p) = rp in
    <| s0 with store_map = (Map.insert r p (s0.store_map)) |>
end

module Frame = struct
  open Store

  type key 'k = Key of 'k

  val dest_key : forall 'k.
    key 'k ->
    'k
  let dest_key (Key k) = k

  type value 'v = Value of 'v
  declare {isabelle} rename type value = value_t

  val dest_value : forall 'v.
    value 'v ->
    'v
  let dest_value v = (match v with Value v -> v end)

  type node_frame 'r 'k =
    <|
      nf_n  : nat;                (* number of keys *)
      nf_ks : nat -> key 'k;      (* [nf_ks] returns the key for the given index *)
      nf_rs : nat -> page_ref 'r; (* [nf_rs] returns the page_ref for the given index --there should be nf_n page_refs stored in a [node_frame] *)
      |>

  type leaf_frame 'k 'v =
    <|
      lf_kvs : map (key 'k) (value 'v);
    |>

  type frame 'r 'k 'v = Frm_I of node_frame 'r 'k | Frm_L of leaf_frame 'k 'v
end

module Ctxt = struct
  open Store
  open Frame

  (** page_to_frame *)
  
  (* note that this forces that the page internally stores its type;
  this is not necessary, but is used by step_find *)
  type page_to_frame 'bs 'k 'r 'v = P2f of (page 'bs -> frame 'r 'k 'v)

  val dest_p2f : forall 'bs 'k 'r 'v.
    page_to_frame 'bs 'k 'r 'v ->
    (page 'bs -> frame 'r 'k 'v)
  let dest_p2f p2f = (match p2f with P2f p2f -> p2f end)


  (** key_to_ref *)

  type key_to_ref 'bs 'k 'r 'v = K2r of (node_frame 'r 'k -> key 'k -> page_ref 'r)

  val dest_k2r : forall 'bs 'k 'r 'v.
    key_to_ref 'bs 'k 'r 'v ->
    (node_frame 'r 'k -> key 'k -> page_ref 'r)
  let dest_k2r k2r = (match k2r with K2r k2r -> k2r end)

  (** frame_to_page *)

  type frame_to_page 'bs 'k 'r 'v = F2p of (frame 'r 'k 'v -> page 'bs)

  val dest_f2p : forall 'bs 'k 'r 'v.
    frame_to_page 'bs 'k 'r 'v ->
    frame 'r 'k 'v ->
    page 'bs
  let dest_f2p f2p = (match f2p with F2p f2p -> f2p end)
    
  type ctxt_t 'bs 'k 'r 'v =
    <|
      ctxt_p2f         : page_to_frame 'bs 'k 'r 'v;
      ctxt_k2r         : key_to_ref    'bs 'k 'r 'v;
      ctxt_f2p         : frame_to_page 'bs 'k 'r 'v;
      ctxt_max_fanout  : nat;
      ctxt_free_p_ref  : store 'r 'bs -> store 'r 'bs;
      ctxt_new_nf      : node_frame 'r 'k;
      (* [ctxt_add_k_pr_nf m_k r nf] adds the relation (0,r) to the nf
      if the m_k is None, elsewhere it adds the relation (k,r) at
      the index of the ordered index of k (where k = fromJust m_k)*)
      ctxt_add_k_pr_nf : (maybe (key 'k)) -> page_ref 'r -> node_frame 'r 'k -> node_frame 'r 'k;
      (* [sust_page_ref_nf old new nf] substitutes old page_ref with new, if old exists in nf *)
      ctxt_subst_pr_nf : page_ref 'r -> page_ref 'r -> node_frame 'r 'k -> node_frame 'r 'k;
      (* [splitl] halves the leaf map on the median key (in the implementation this can be done using the order of the keys) *)
      ctxt_splitL : map (key 'k) (value 'v) -> ((map (key 'k) (value 'v)) * key 'k * (map (key 'k) (value 'v)));
      (* [splitI] halves the inode *)
      ctxt_splitI : node_frame 'r 'k -> (node_frame 'r 'k * key 'k * node_frame 'r 'k);                                                 
    |>
end

module PageRef2Frame = struct
  (* interpretation of pages *)
  open Store
  open Frame
  open Ctxt

  val page_ref_to_frame : forall 'bs 'k 'r 'v.
    ctxt_t 'bs 'k 'r 'v ->
    store 'r 'bs ->
    page_ref 'r ->
    maybe (frame 'r 'k 'v)
  let page_ref_to_frame ctxt s0 r0 = (
      match ref_to_page s0 r0 with
      | Nothing -> Nothing
      | Just p ->
         let p2f = dest_p2f (ctxt.ctxt_p2f) in
         Just (p2f p)
      end
    )
end

module Btree = struct
  open Frame
  open Prelude
  open PageRef2Frame
  open Ctxt       
  open Store

  type btree 'k 'v =
    Tr_nd of (nat * (nat -> key 'k) * (nat -> btree 'k 'v))
    | Tr_lf of (map (key 'k) (value 'v))

  type tree_height = nat

  val btree_to_kvs : forall 'k 'v.
    btree 'k 'v ->
    map (key 'k)  (value 'v)
  let rec btree_to_kvs t = (
      match t with
      | Tr_lf kvs -> kvs
      | Tr_nd (nk,_,n2t) ->
         let subtrees = (List.map n2t (from_n_to_n' 0 (nk + 1))) in
         let subtrees_kvs = List.map btree_to_kvs subtrees in
         Map.unions subtrees_kvs
      end)

  (* NB this has an explicit n argument, whereas wfness gives us that we can just use page_ref_to_frame *)
  val page_ref_to_btree : forall 'bs 'k 'r 'v.
    ctxt_t 'bs 'k 'r 'v ->
    store 'r 'bs ->
    page_ref 'r ->
    tree_height ->
    maybe (btree 'k 'v)
  let rec page_ref_to_btree ctxt s0 r0 n = (
      match n with
      | 0  -> (
        match page_ref_to_frame ctxt s0 r0 with
        | Nothing -> (rresult_to_option (Error))
        | Just frm -> (
          match frm with
          | Frm_L lf -> Just (Tr_lf (lf.lf_kvs))
          | _ -> (rresult_to_option (Error)) (* attempt to access missing page *)
          end
        )
        end
      )
      | n_plus_one ->
         let n' = n_plus_one - 1 in
         (match page_ref_to_frame ctxt s0 r0 with
          | Nothing -> (rresult_to_option (Error))
          | Just frm -> (
            match frm with
            | Frm_I nf ->
               let n  = nf.nf_n  in
               let ks = nf.nf_ks in
               let rs = nf.nf_rs in
               let (f0:(page_ref 'r -> maybe (btree 'k 'v))) =
                 (fun r ->  page_ref_to_btree ctxt s0 r n')
               in
               let prop =
                 let refs_to_trees = List.map rs (from_n_to_n' 0 (n + 1)) in
                 let subtrees = List.map f0 refs_to_trees in
                 (List.all isJust subtrees) && (n > 0)
               in
               (match prop with
                | true ->
                   let subtree =
                     (fun (m:nat) -> fromJust (f0 (rs m)))
                   in
                   Just(Tr_nd(n,ks,subtree))
                | _ -> (rresult_to_option (Error)) (* Frm_I was not wellformed - prop was false *)
                end)
            | _ -> (rresult_to_option (Error)) (* found Frm_L but tree_height was not 0 *)
            end)
          end)
      end)

  val page_ref_to_kvs : forall 'bs 'k 'r 'v.
    ctxt_t 'bs 'k 'r 'v ->
    store 'r 'bs ->
    page_ref 'r ->
    tree_height ->
    maybe (map (key 'k) (value 'v))
  let page_ref_to_kvs ctxt s0 r0 n0 = (
      match page_ref_to_btree ctxt s0 r0 n0 with
      | Just t -> Just (btree_to_kvs t)
      | _ -> Nothing
      end)

  val page_ref_key_to_v : forall 'bs 'k 'r 'v.
    ctxt_t 'bs 'k 'r 'v ->
    store 'r 'bs ->
    page_ref 'r ->
    tree_height ->
    key 'k ->
    maybe (value 'v)
  let page_ref_key_to_v ctxt s0 r0 n0 k0 =
    let m0 = page_ref_to_kvs ctxt s0 r0 n0 in
    Maybe.bind m0 (Map.lookup k0)

  val key_to_v : forall 'r 'k 'v.
    leaf_frame 'k 'v ->
    key 'k ->
    maybe (value 'v)
  let key_to_v lf k = Map.lookup k lf.lf_kvs

end

module Find = struct
  open Frame
  open Store
  open Prelude
  open PageRef2Frame
  open Ctxt
  open Btree

  type find_state_l 'bs 'k 'r 'v =
    <|
      fsl_k : key 'k;
      fsl_r : page_ref 'r;
    |>

  type find_state_r 'bs 'k 'r 'v =
    <|
      fsr_r : page_ref 'r;
      fsr_v : maybe (value 'v);
    |>

  type find_state 'bs 'k 'r 'v =
    Fs_l of find_state_l  'bs 'k 'r 'v
    | Fs_r of find_state_r  'bs 'k 'r 'v


  val fs_step : forall 'bs 'k 'r 'v.
    ctxt_t 'bs 'k 'r 'v ->
    ((store 'r 'bs) * (find_state 'bs 'k 'r 'v)) ->
    maybe ((store 'r 'bs) * (find_state 'bs 'k 'r 'v))
  let fs_step ctxt1 s0fs0 = (
      let (s0,fs0) = s0fs0 in
      match fs0 with
      | Fs_l fsl ->
         let r0 = fsl.fsl_r in
         let k0 = fsl.fsl_k in
         (match (page_ref_to_frame ctxt1 s0 r0) with
          | Nothing -> rresult_to_option Error (* invalid page access *)
          | Just frm ->
             (match frm with
              | Frm_I nf ->
                 let k2r = dest_k2r (ctxt1.ctxt_k2r) in
                 let r'  = k2r nf k0 in
                 Just (s0, Fs_l (<|fsl with fsl_r = r'|>))
              | Frm_L lf ->
                 let k2v = key_to_v in
                 let v = k2v lf k0 in
                 Just (s0, Fs_r <| fsr_r = r0; fsr_v = v|>)
              end)
          end)
      | Fs_r _ -> rresult_to_option Error (* attempt to step Fs_r *)
      end
    )


  val fs_step_as_fun : forall 'bs 'k 'r 'v.
    ctxt_t 'bs 'k 'r 'v ->
    tree_height ->
    ((store 'r 'bs) * (find_state 'bs 'k 'r 'v)) ->
    ((store 'r 'bs) * (find_state 'bs 'k 'r 'v))
  let rec fs_step_as_fun ctxt1 n0 s0fs0 = (
      let s0fs1 = fromJust (fs_step ctxt1 s0fs0) in
      (if n0 = 0
      then s0fs1
      else fs_step_as_fun ctxt1 (n0 - 1) (s0fs1))
    )
end

module Insert = struct
  open Frame
  open Store
  open Prelude
  open PageRef2Frame
  open Ctxt
  open Btree

  type ins_command 'k 'r 'v = Insert | S | D of (key 'k * page_ref 'r) | Ret

  type ins_state 'k 'r 'v =
    <|
      ins_comm      : ins_command 'k 'r 'v;
      ins_kv        : (key 'k * value 'v);
      ins_r         : page_ref 'r;
      ins_pi        : list (page_ref 'r * node_frame 'r 'k);
      ins_is_taller : bool;
    |>


  (** descending insert ops *)

  val dup_descend_nf_ref : forall 'bs 'k 'r 'v.
    ctxt_t 'bs 'k 'r 'v ->
    ((store 'r 'bs) * (ins_state 'k 'r 'v) * (list(page_ref 'r))) ->
    node_frame 'r 'k ->
    ((store 'r 'bs) * (ins_state 'k 'r 'v) * (list(page_ref 'r)))
  let dup_descend_nf_ref ctxt conf nf =
    let key2ref = dest_k2r (ctxt.ctxt_k2r) in
    let (s0,ins_state,old_rs) = conf in
    let r0 = ins_state.ins_r in
    let pi = ins_state.ins_pi in
    let (k,_) = ins_state.ins_kv in
    let r1 = key2ref nf k in
    let s1 = ctxt.ctxt_free_p_ref s0 in
    let r0' = s1.store_last_free_ref in
    (* we add the new page_ref to pi *)
    (* we keep navigating using r1 *)
    let ins_state' = <|ins_state with ins_r=r1; ins_pi=(r0',nf)::(pi); ins_is_taller=false|> in
    (s1,ins_state',(r0::old_rs))


  val dup_insert_in_lf : forall 'bs 'k 'r 'v.
    ctxt_t 'bs 'k 'r 'v ->
    ((store 'r 'bs) * (ins_state 'k 'r 'v) * (list(page_ref 'r))) ->
    leaf_frame 'k 'v ->
    ((store 'r 'bs) * (ins_state 'k 'r 'v) * (list(page_ref 'r)))
  let dup_insert_in_lf ctxt conf lf =
    let frame2page = dest_f2p (ctxt.ctxt_f2p) in
    let (s0,ins_state,old_rs) = conf in
    let r0 = ins_state.ins_r in
    let (k,v) = ins_state.ins_kv in
    (* we prepare the new page_ref *)
    let s1 = ctxt.ctxt_free_p_ref s0 in
    let r0' = s1.store_last_free_ref in
    let kvs' = Map.insert k v (lf.lf_kvs) in
    (* have we filled the leaf over the limit?*)
    if (Map.size kvs' <= ctxt.ctxt_max_fanout)
    (* not over the limit *)
    then
      let s2 = insert_store s1 (r0',frame2page (Frm_L <|lf_kvs = kvs'|>)) in
      (* in the second pass rules don't use ins_r, so we pass the r0'
            in it (we'll need to substitute a node of the parent frame
            with it during the ascending phase) *)
      let ins_state' = <|ins_state with ins_r=r0'; ins_comm=S; ins_is_taller=false|> in
      (s2,ins_state',(r0::old_rs))
    (* full over the limit  --there are going to be splits *)
    else
      let (kvs',median_k,kvs'') = ctxt.ctxt_splitL kvs' in
      let s2 = ctxt.ctxt_free_p_ref s1 in
      let q = s2.store_last_free_ref in
      let s3 = insert_store s2 (r0',frame2page (Frm_L <|lf_kvs = kvs'|>)) in
      let s4 = insert_store s3 (q, frame2page (Frm_L <|lf_kvs = kvs''|>)) in
      (* in the second pass rules don't use ins_r, so we pass the r0'
            in it (we'll need to substitute a node of the parent frame
            with it during the ascending phase) *)
      let ins_state' = <|ins_state with ins_r=r0'; ins_comm=D(median_k,q); ins_is_taller=false|> in
      (s4,ins_state',(r0::old_rs))
  
  val descending_insert : forall 'bs 'k 'r 'v.
    ctxt_t 'bs 'k 'r 'v ->
    ((store 'r 'bs) * (ins_state 'k 'r 'v) * (list(page_ref 'r))) ->
    maybe ((store 'r 'bs) * (ins_state 'k 'r 'v) * (list(page_ref 'r)))
  let descending_insert ctxt conf =
    let (s0,ins_state,_) = conf in
    let ins_c  = ins_state.ins_comm in
    let r0 = ins_state.ins_r in
    (match ins_c with
     (** descending the tree*)
     | Insert ->
      (match page_ref_to_frame ctxt s0 r0 with
       | Nothing -> (rresult_to_option (Error)) (*attempt to access unexisting page*)
       (* we found a node: we add a new page_ref to pi for duplication
       and keep navigating the path *)
       | Just (Frm_I nf) -> Just (dup_descend_nf_ref ctxt conf nf)
       (* we found a leaf: we can insert our entry (k,v)*)
       | Just (Frm_L lf) -> Just (dup_insert_in_lf ctxt conf lf)
       end)
     | _ -> rresult_to_option Error (* only Insert command allowed when descending *)
     end)

  (** ascending insert ops *)

  val dup_ascend_tree_without_splits : forall 'bs 'k 'r 'v.
    ctxt_t 'bs 'k 'r 'v ->
    ((store 'r 'bs) * (ins_state 'k 'r 'v) * (list(page_ref 'r))) ->
    maybe ((store 'r 'bs) * (ins_state 'k 'r 'v) * (list(page_ref 'r)))
  let dup_ascend_tree_without_splits ctxt conf =
    let key2ref = dest_k2r (ctxt.ctxt_k2r) in
    let frame2page = dest_f2p (ctxt.ctxt_f2p) in
    let (s0,ins_state,old_rs) = conf in
    let r0 = ins_state.ins_r in
    let pi = ins_state.ins_pi in
    let (k,_) = ins_state.ins_kv in
    (match pi with
     (* we reached the top of the tree, we can return*)
     | [] -> Just (s0,<|ins_state with ins_comm=Ret|>,old_rs)
     (* we are ascending the tree *)
     | (t,nf)::pi' ->
        (* we need to update the content of the parent frame *)
        (* we get the old ref for the child: nf is the content of
            the old parent, so it gets the same page_ref that conduced
            us here*)
        let old_r = key2ref nf k in
        (* and we substitute old_r with r0 in nf *)
        let nf' = ctxt.ctxt_subst_pr_nf old_r r0 nf in
        (* finally we insert the parent in the store *)
        let s1 = insert_store s0 (t,frame2page (Frm_I nf')) in
        let ins_state' = <|ins_state with ins_pi=pi'; ins_r=t; ins_is_taller=false|> in
        Just(s1,ins_state',old_rs)
     end)

  val dup_grow_tree : forall 'bs 'k 'r 'v.
    ctxt_t 'bs 'k 'r 'v ->
    ((store 'r 'bs) * (ins_state 'k 'r 'v) * (list(page_ref 'r))) ->
    key 'k ->
    page_ref 'r ->
    maybe ((store 'r 'bs) * (ins_state 'k 'r 'v) * (list(page_ref 'r)))
  let dup_grow_tree ctxt conf median_k q =
    let frame2page = dest_f2p (ctxt.ctxt_f2p) in
    let (s0,ins_state,old_rs) = conf in
    let r0 = ins_state.ins_r in
    let s1 = ctxt.ctxt_free_p_ref s0 in
    let r0' = s1.store_last_free_ref in
    let nf = (ctxt.ctxt_new_nf) in
    (* add old root r0 -that is duplicated old root- as only
            child of nf *)
    let nf' = (ctxt.ctxt_add_k_pr_nf Nothing r0 nf) in
    (* add sibling after the median_k *)
    let nf''= (ctxt.ctxt_add_k_pr_nf (Just median_k) q  nf') in
    let s2 = insert_store s1 (r0',frame2page (Frm_I nf'')) in
    Just (s2,<|ins_state with ins_r=r0'; ins_comm=Ret; ins_is_taller=true|>,old_rs)

  val dup_ascend_split_tree : forall 'bs 'k 'r 'v.
    ctxt_t 'bs 'k 'r 'v ->
    ((store 'r 'bs) * (ins_state 'k 'r 'v) * (list(page_ref 'r))) ->
    key 'k ->
    page_ref 'r ->
    maybe ((store 'r 'bs) * (ins_state 'k 'r 'v) * (list(page_ref 'r)))
  let dup_ascend_split_tree ctxt conf median_k q =
    let key2ref = dest_k2r (ctxt.ctxt_k2r) in
    let frame2page = dest_f2p (ctxt.ctxt_f2p) in
    let (s0,ins_state,old_rs) = conf in
    let r0 = ins_state.ins_r in
    let pi = ins_state.ins_pi in
    let (k,_) = ins_state.ins_kv in
    (match pi with
     (* we arrived at the top of the tree with a D: we need a
         taller tree *)
     | [] -> dup_grow_tree ctxt conf median_k q
     (* we are ascending the tree after a split happened *)
     | (t,nf)::pi' ->
        if  (nf.nf_n + 1) < ctxt.ctxt_max_fanout
                              (* no other split needed *)
        then
          let old_r = key2ref nf k in
          (* and we substitute old_r with r0 in nf *)
          let nf' = ctxt.ctxt_subst_pr_nf old_r r0 nf in
          (* we add the median key and the page_ref pointing to
              the second half of the old_r contents*)
          let nf'' = ctxt.ctxt_add_k_pr_nf (Just median_k) q nf' in 
          (* finally we insert the parent in the store *)
          let s1 = insert_store s0 (t,frame2page (Frm_I nf'')) in
          let ins_state' = <|ins_state with ins_pi=pi'; ins_r=t; ins_is_taller=false|> in
          Just(s1,ins_state',old_rs)
              (* we need to split the parent node *)
        else
          let old_r = key2ref nf k in
          (* and we substitute old_r with r0 in nf *)
          let nf' = ctxt.ctxt_subst_pr_nf old_r r0 nf in
          (* we add the median key and the page_ref pointing to
              the second half of the old_r contents*)
          let nf'' = ctxt.ctxt_add_k_pr_nf (Just median_k) q nf' in
          let (nf1,median_k',nf2) = splitI ctxt (nf'') in
          (* we insert the parent in the store with the page_ref
              pointing to the first half of the old parent contents *)
          let s1 = insert_store s0 (t,frame2page (Frm_I nf1)) in
          (* we insert a new page_ref parent in the store for the
              second half of the old parent contents *)
          let s2 = ctxt.ctxt_free_p_ref s1 in
          let q' = s2.store_last_free_ref in
          let s3 = insert_store s2 (q',frame2page (Frm_I nf2)) in
          let ins_state' = <|ins_state with ins_comm=D(median_k',q'); ins_pi=pi'; ins_r=t; ins_is_taller=false|> in
          Just(s3,ins_state',old_rs)
     end)

  val ascending_insert : forall 'bs 'k 'r 'v.
    ctxt_t 'bs 'k 'r 'v ->
    ((store 'r 'bs) * (ins_state 'k 'r 'v) * (list(page_ref 'r))) ->
    maybe ((store 'r 'bs) * (ins_state 'k 'r 'v) * (list(page_ref 'r)))
  let ascending_insert ctxt conf =
    let (_,ins_state,_) = conf in
    let ins_c  = ins_state.ins_comm in
    (match ins_c with
     | S -> dup_ascend_tree_without_splits ctxt conf
     (* we split the tree, we need to check that we do not need
     further splits *)
     | D(median_k,q) -> dup_ascend_split_tree ctxt conf median_k q
     | _ -> (rresult_to_option (Error)) (*attempt to step Ret or Insert *) 
     end)

  (** insert ops *)

  val insert_step : forall 'bs 'k 'r 'v.
    ctxt_t 'bs 'k 'r 'v ->
    ((store 'r 'bs) * (ins_state 'k 'r 'v) * (list(page_ref 'r))) ->
    maybe ((store 'r 'bs) * (ins_state 'k 'r 'v) * (list(page_ref 'r)))
  let insert_step ctxt conf =
    let (_,ins_state,_) = conf in
    let ins_c  = ins_state.ins_comm in
    (* insert does not work with nodes with a capacity less than 2*)
    (match ins_c with
     (** descending the tree*)
     | Insert -> descending_insert ctxt conf
     (** ascending the tree *)
     | _ -> ascending_insert ctxt conf
     end)

  val insert_step_as_fun : forall 'bs 'k 'r 'v.
    ctxt_t 'bs 'k 'r 'v ->
    ((store 'r 'bs) * (ins_state 'k 'r 'v) * (list(page_ref 'r))) ->
    ((store 'r 'bs) * (ins_state 'k 'r 'v) * (list(page_ref 'r)))
  let rec insert_step_as_fun ctxt (s0,ins0,old_rs) = (
    match (ins0.ins_comm) with
    | Ret -> (s0,ins0,old_rs)
    | _ ->
       (match insert_step ctxt (s0,ins0,old_rs) with
        | Nothing -> failwith "the store was not valid: unexisting page_ref!"
        | Just conf' -> insert_step_as_fun ctxt conf'
        end)
    end)

end
